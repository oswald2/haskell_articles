\chapter{The Handle Pattern - Jasper van der Joigt}

\begin{quotation}
\noindent\textit{\textbf{William Yao:}}

\textit{While there are fancy ways of injecting things like DB access and side effects into your application, such as monad transformers or effect algebras, the dead-simplest way to avoid turning your program into IO-soup is to just pass around records of functions. While there's nothing wrong with just leaving things in IO when bootstrapping a new project, if you find yourself needing to abstract over your concrete effects, try reaching for this before something more complicated.}

\vspace{\baselineskip}
\noindent\textit{Original article: \cite{the_handle_pattern}}
\end{quotation}

\section{Introduction}

I'd like to talk about a design pattern in Haskell that I've been
calling \emph{the Handle pattern}. This is far from novel -- I've
mentioned this
\href{https://skillsmatter.com/skillscasts/10832-how-to-architect-medium-to-large-scale-haskell-applications}{before}
and the idea is definitely not mine. As far as I know, in fact, it has
been around since basically
forever\footnote{Well, \texttt{System.IO.Handle} has definitely been around for a while}.
Since it is ridiculously close to what we'd call \emph{common
sense}\footnote{If you're reading this article and you're thinking: \emph{``What does
  this guy keep going on about? This is all so obvious!''} -- Well,
  that's the
  point!},
it's often used without giving it any explicit thought.

I first started more consciously using this pattern when I was working
together with \href{https://github.com/meiersi}{Simon Meier} at Better
(aka
\href{https://jaspervdj.be/posts/2013-09-29-erudify.html}{erudify}).
Simon did a writeup about this pattern
\href{https://www.schoolofhaskell.com/user/meiersi/the-service-pattern}{as
well}. But as I was explaining this idea again at last week's
\href{https://www.meetup.com/HaskellerZ/}{HaskellerZ} meetup, I figured
it was time to do an update of that article.

The \emph{Handle pattern} allows you write stateful applications that
interact with external services in Haskell. It complements pure code
(e.g.~your business logic) well, and it is somewhat the result of
iteratively applying the question:

\begin{itemize}

\item
  Can we make it simpler?
\item
  Can we make it simpler still?
\item
  And can we still make it simpler?
\end{itemize}

The result is a powerful and simple pattern that does not even require
Monads\footnote{It does require \texttt{IO}, but we don't require thinking about
  \texttt{IO} as a \texttt{Monad}. If this sounds weird -- think of
  lists. We work with lists all the time but we just consider them lists
  of things, we don't constantly call them ``List Monad'' or ``The Free
  Monoid'' for that
  matter.}
or Monad transformers to be useful. This makes it extremely suitable for
beginners trying to build their first medium-sized Haskell application.
And that does not mean it is beginners-only: this technique has been
applied successfully at several Haskell companies as well.


\section{Context}

In Haskell, we try to capture ideas in beautiful, pure and
mathematically sound patterns, for example \emph{Monoids}. But at other
times, we can't do that. We might be dealing with some inherently
mutable state, or we are simply dealing with external code which doesn't
behave nicely.

In those cases, we need another approach. What we're going to describe
feels suspiciously similar to Object Oriented Programming:

\begin{itemize}

\item
  Encapsulating and hiding state inside objects
\item
  Providing methods to manipulate this state rather than touching it
  directly
\item
  Coupling these objects together with methods that modify their state
\end{itemize}

As you can see, it is not exactly the same as Alan Kay's
\href{http://wiki.c2.com/?AlanKaysDefinitionOfObjectOriented}{original
definition} of
OOP\footnote{And indeed, we will touch on a common way of encoding OOP in Haskell
  -- creating explicit records of functions -- but we'll also explain
  why this isn't always
  necessary.},
but it is far from the horrible incidents that permeate our field such
as UML, abstract factory factories and broken subtyping.

Before we dig in to the actual code, let's talk about some disclaimers.

Pretty much any sort of Haskell code can be written in this particular
way, but \emph{that doesn't mean that you should}. This method relies
heavily on \texttt{IO}. Whenever you can write things in a pure way, you
should attempt to do that and avoid \texttt{IO}. This pattern is only
useful when \texttt{IO} is required.

Secondly, there are many alternatives to this approach: complex monad
transformer stacks, interpreters over free monads, uniqueness types,
effect systems\ldots{} I don't want to claim that this method is better
than the others. All of these have advantages and disadvantages, so one
must always make a careful trade-off.

\hypertarget{the-module-layout}{%
\subsection{The module layout}\label{the-module-layout}}

For this pattern, we've got a very well-defined module layout. I believe
this helps with recognition which I think is also one of the reasons we
use typeclasses like \emph{Monoid}.

When I'm looking at the documentation of libraries I haven't used yet,
the types will sometimes look a bit bewildering. But then I see that
there's an \texttt{instance\ Monoid}. That's an ``Aha!'' moment for me.
I \emph{know} what a Monoid is. I \emph{know} how they behave. This
allows me to get up to speed with this library much faster!

Using a consistent module layout in a project (and even across projects)
provides, I think, very similar benefits to that. It allows new people
on the team to learn parts of the codebase they are yet unfamiliar with
much faster.

\subsection{A Database Handle}

Anyway, let's look at the concrete module layout we are proposing with
this pattern. As an example, let's consider a database. The type in
which we are encapsulating the state is \emph{always} called
\texttt{Handle}. That is because we
\href{https://mail.haskell.org/pipermail/haskell-cafe/2008-June/043986.html}{design
for qualified import}.

We might have something like:

\begin{minted}{haskell}
module MyApp.Database

data Handle = Handle
    { hPool   :: Pool Postgres.Connection
    , hCache  :: IORef (PSQueue Int Text User)
    , hLogger :: Logger.Handle  -- Another handle!
    , ...
    }
\end{minted}
The internals of the \texttt{Handle} typically consist of static fields
and other handles, \texttt{MVar}s, \texttt{IORef}s, \texttt{TVar}s,
\texttt{Chan}s\ldots{} With our \texttt{Handle} defined, we are able to
define functions using it. These are usually straightforward imperative
pieces of code and I'll omit them for
brevity\footnote{If you want to see a full example, you can refer to
  \href{https://github.com/jaspervdj/fugacious}{this repository} that I
  have been using to teach practical
  Haskell.}:

\begin{minted}{haskell}
module MyApp.Database where

data Handle = ...

createUser :: Handle -> Text -> IO User
createUser = ...

getUserMail :: Handle -> User -> IO [Mail]
getUserMail = ...
\end{minted}
Some thoughts on this design:

\begin{enumerate}
\item
  We call our functions \texttt{createUser} rather than
  \texttt{databaseCreateUser}. Again, we're working with qualified
  imports so there's no need for ``C-style'' names.
\item
  \textbf{All functions take the \texttt{Handle} as the first argument.}
  This is very important for consistency, but also for
  \href{https://jaspervdj.be/posts/2018-03-08-handle-pattern.html\#handle-polymorphism}{polymorphism}
  and code style.

  With code style, I mean that the \texttt{Handle} is often a
  syntactically simpler expression (e.g.~a name) than the argument
  (which is often a composed expression). Consider:

\begin{minted}{haskell}
Database.createUser database $ userName <> "@" <> companyDomain
\end{minted}
  Versus:

\begin{minted}{haskell}
Database.createUser (userName <> "@" <> companyDomain) database
\end{minted}
\item
  Other \texttt{Handle}s (e.g.~\texttt{Logger.Handle}) are stored in a
  field of our \texttt{Database.Handle}. You could also remove it there
  and instead have it as an argument wherever it is needed, for example:

\begin{minted}{haskell}
createUser :: Handle -> Logger.Handle -> Text -> IO User
createUser = ...
\end{minted}
  I usually prefer to put it inside the \texttt{Handle} since that
  reduces the amount of arguments required for functions such as
  \texttt{createUser}. However, if the lifetime of a
  \texttt{Logger.Handle} is very
  short\href{https://jaspervdj.be/posts/2018-03-08-handle-pattern.html\#fn6}{\textsuperscript{6}},
  or if you want to reduce the amount of dependencies for \texttt{new},
  then you could consider doing the above.
\item
  The datatypes such as \texttt{Mail} may be defined in this module may
  even be specific to this function. I've written about
  ad-hoc datatypes before (see chapter \ref{sec:adhoc_datatypes}).
\end{enumerate}

\subsection{Creating a Handle}

I mentioned before that an important advantage of using these patterns
is that programmers become ``familiar'' with it. That is also the goal
we have in mind when designing our API for the creation of
\texttt{Handle}s.

In addition to always having a type called \texttt{Handle}, we'll
require the module to always have a type called \texttt{Config}. This is
where we encode our static configuration parameters -- and by static I
mean that we shouldn't have any \texttt{IORef}s or the like here: this
\texttt{Config} should be easy to create from pure code.

\begin{minted}{haskell}
module MyApp.Database where

data Config = Config
    { cPath :: FilePath
    , ...
    }

data Handle = ...
\end{minted}
We can also offer some way to create a \texttt{Config}. This really
depends on your application. If you use the
\href{https://hackage.haskell.org/package/configurator}{configurator}
library, you might have something like:

\begin{minted}{haskell}
parseConfig :: Configurator.Config -> IO Config
parseConfig = ...
\end{minted}
On the other hand, if you use
\href{https://hackage.haskell.org/package/aeson}{aeson} or
\href{https://hackage.haskell.org/package/yaml}{yaml}, you could write:

\begin{minted}{haskell}
instance Aeson.FromJSON Config where
    parseJSON = ...
\end{minted}
You could even use a
\href{https://medium.com/@jonathangfischoff/the-partial-options-monoid-pattern-31914a71fc67}{Monoid}
to support loading configurations from multiple places. But I digress --
the important part is that there is a type called \texttt{Config}.

Next is a similar pattern: in addition to always having a
\texttt{Config}, we'll also always provide a function called
\texttt{new}. The parameters follow a similarly strict pattern:

\begin{minted}{haskell}
new :: Config         -- 1. Config
    -> Logger.Handle  -- 2. Dependencies
    -> ...            --    (usually other handles)
    -> IO Handle      -- 3. Result
\end{minted}
Inside the \texttt{new} function, we can create some more
\texttt{IORef}s, file handles, caches\ldots{} if required and then store
them in the \texttt{Handle}.

\subsection{Destroying a Handle}

We've talked about creation of a \texttt{Handle}, and we mentioned the
normal functions operating on a \texttt{Handle}
(e.g.~\texttt{createUser}) before. So now let's consider the final stage
in the lifetime of \texttt{Handle}.

Haskell is a garbage collected language and we can let the runtime
system take care of destroying things for us -- but that's not always a
great idea. Many resources (file handles in particular come to mind as
an example) are scarce.

There is quite a strong correlation between scarce resources and things
you would naturally use a \texttt{Handle} for. That's why I recommend
always providing a \texttt{close} as well, even if does nothing. This is
a form of forward compatibility in our API: if we later decide to add
some sort of log files (which will need to be closed), we can do so
without individually mailing all our module users that they now need to
add a \texttt{close} to their code.

\begin{minted}{haskell}
close :: Handle -> IO ()
close = ...
\end{minted}




\subsection{Reasonable safety}

When you're given a \texttt{new} and \texttt{close}, it's often tempting
to add an auxiliary function like:

\begin{minted}{haskell}
withHandle
    :: Config            -- 1. Config
    -> Logger.Handle     -- 2. Dependencies
    -> ...               --    (usually other handles)
    -> (Handle -> IO a)  -- 3. Function to apply
    -> IO a              -- 4. Result, handle is closed automatically
\end{minted}
I think this is a great idea. In fact, it's sometimes useful to
\emph{only} provide the \texttt{withHandle} function, and hide
\texttt{new} and \texttt{close} in an internal module.

The only caveat is that the naive implementation of this function:

\begin{minted}{haskell}
withHandle config dep1 dep2 ... depN f = do
    h <- new config dep1 dep2 ... depN
    x <- f h
    close h
    return x
\end{minted}
Is \textbf{wrong}! In any sort of \texttt{withXyz} function, you should
always use \texttt{bracket} to guard against exceptions. This means the
correct implementation is:

\begin{minted}{haskell}
withHandle config dep1 dep2 … depN f =
    bracket (new config dep1 dep2 … depN) close f
\end{minted}
Well, it's even shorter! In case you want more information on why
\texttt{bracket} is necessary,
\href{http://www.well-typed.com/blog/97/}{this blogpost} gives a good
in-depth overview. My summary of it as it relates to this article would
be:

\begin{enumerate}
\item
  Always use \texttt{bracket} to match \texttt{new} and \texttt{close}
\item
  You can now use \texttt{throwIO} and \texttt{killThread} safely
\end{enumerate}
It's important to note that \texttt{withXyz} functions do not provide
complete safety against things like use-after-close or double-close.
There are many interesting approaches to fix these issues but they are
\emph{way} beyond the scope of this tutorial -- things like
\href{http://okmij.org/ftp/Haskell/regions.html}{Monadic Regions} and
\href{https://www.cis.upenn.edu/~jpaykin/papers/pz_linearity_monad_2017.pdf}{The
Linearity Monad} come to mind. For now, we'll rely on \texttt{bracket}
to catch common issues and on code reviews to catch team members who are
not using \texttt{bracket}.

\subsection{Summary of the module layout}

If we quickly summarise the module layout, we now have:

\begin{minted}{haskell}
module MyApp.Database
    ( Config (..)   -- Internals exported
    , parseConfig   -- Or some other way to load a config

    , Handle        -- Internals usually not exported
    , new
    , close
    , withHandle

    , createUser  -- Actual functions on the handle
    , ...
    ) where
\end{minted}
This is a well-structured, straightforward and easy to learn
organisation. Most of the \texttt{Handle}s in any application should
probably look this way. In the next section, we'll see how we can build
on top of this to create dynamic, customizable \texttt{Handle}s.

\section{Handle polymorphism}

It's often important to split between the interface and implementation
of a service. There are countless ways to do this in programming
languages. For Haskell, there is:

\begin{itemize}

\item
  Higher order functions
\item
  Type classes and type families
\item
  Dictionary passing
\item
  Backpack module system
\item
  Interpreters over concrete ASTs
\item
  \ldots{}
\end{itemize}
The list is endless. And because Haskell on one hand makes it so easy to
abstract over things, and on the other hand makes it possible to
abstract over pretty much anything, I'll start this section with a
disclaimer.

\emph{Premature} abstraction is a real concern in Haskell (and many
other high-level programming languages). It's easy to quickly whiteboard
an abstraction or interface and unintentionally end up with completely
the wrong thing.

It usually goes like this:

\begin{enumerate}
\def\labelenumi{\arabic{enumi}.}

\item
  You need to implement a bunch of things that look similar
\item
  You write down a typeclass or another interface-capturing abstraction
\item
  You start writing the actual implementations
\item
  One of them doesn't \emph{quite} match the interface so you need to
  change it two weeks in
\item
  You add another parameter, or another method, mostly for one specific
  interface
\item
  This causes some problems or inconsistencies for interfaces
\item
  Go back to (4)
\end{enumerate}
What you end up with is a leaky abstraction that is the \emph{product}
of all concrete implementations -- where what you really wanted is the
\emph{greatest common divisor}.

There's no magic bullet to avoid broken abstractions so my advice is
usually to first painstakingly do all the different implementations (or
at least a few of them). \emph{After} you have something working and you
have emerged victorous from horrible battles with the guts of these
implementations, \emph{then} you could start looking at what the
different implementations have in common. At this point, you'll also be
a bit wiser about where they differ -- and you'll be able to take these
important details into account, at which point you retire from just
being an idiot drawing squares and arrows on a whiteboard.

This is why I recommend sticking with simple \texttt{Handle}s until
\href{https://en.wikipedia.org/wiki/You_aren\%27t_gonna_need_it}{you
really need it}. But naturally, sometimes we really need the extra
power.

\subsection{A Handle interface}

So let's do the simplest thing that can possibly work. Consider the
following definition of the \texttt{Handle} we discussed before:

\begin{minted}{haskell}
module MyApp.Database
    ( Handle (..)  -- We now need to export this
    ) where

data Handle = Handle
    { createUser :: Text -> IO User
    , ...
    }
\end{minted}
What's the type of \texttt{createUser} now?

\begin{minted}{haskell}
createUser :: Handle -> Text -> IO User
\end{minted}
It's exactly the same as before! This is pretty much a requirement: it
means we can move our \texttt{Handle}s to this approach when we need it,
not when we envision that we will need it at some point in the future.

\subsection{A Handle implementation}

We can now create a concrete implementation for this abstract
\texttt{Handle} type. We'll do this in a module like
\texttt{MyApp.Database.Postgres}.

\begin{minted}{haskell}
module MyApp.Database.Postgres where
import MyApp.Database

data Config = ...

new :: Config -> Logger.Handle -> … -> IO Handle
\end{minted}
The \texttt{Config} datatype and the \texttt{new} function have now
moved to the implementation module, rather than the interface module.

Since we can have any number of implementation modules, it is worth
mentioning that we will have multiple \texttt{Config} types and
\texttt{new} functions (exactly one of each per implementation).
Configurations are always specific to the concrete implementation. For
example, an \href{https://sqlite.org/index.html}{sqlite} database may
just have a \texttt{FilePath} in the configuration, but our
\texttt{Postgres} implementation will have other details such as port,
database, username and password.

In the implementation of \texttt{new}, we simply initialize a
\texttt{Handle}:

\begin{minted}{haskell}
new config dep1 dep2 ... depN = do
    -- Intialization of things inside the handle
    ...

    -- Construct record
    return Handle
        { createUser = \name -> do
            ...
        , ...
        }
\end{minted}
Of course, we can manually float out the body of \texttt{createUser}
since constructing these large records gets kind of ugly.

\section{Compared to other approaches}

We've presented an approach to modularize the effectful layer of medium-
to large-scaled Haskell applications. There are many other approaches to
tackling this, so any comparison I come up with would probably be
inexhaustive.

Perhaps the most important advantage of using \texttt{Handle}s is that
they are first class values that we can freely mix and match. This often
does not come for free when using more exotic strategies.

Consider the following type signature from a Hackage package -- and I do
not mean to discredit the author, the package works fine but simply uses
a different approach than my personal preference:

\begin{minted}{haskell}
-- | Create JSON-RPC session around conduits from transport layer.
-- When context exits session disappears.
runJsonRpcT
    :: (MonadLoggerIO m, MonadBaseControl IO m)
    => Ver                  -- ^ JSON-RPC version
    -> Bool                 -- ^ Ignore incoming requests/notifs
    -> Sink ByteString m () -- ^ Sink to send messages
    -> Source m ByteString  -- ^ Source to receive messages from
    -> JsonRpcT m a         -- ^ JSON-RPC action
    -> m a                  -- ^ Output of action
\end{minted}
I'm a fairly experienced Haskeller and it still takes me a bit of
eye-squinting to see how this will fit into my application, especially
if I want to use this package with other libraries that do not use the
\texttt{Sink}/\texttt{Source} or \texttt{MonadBaseControl} abstractions.

It is somewhat obvious that one running call to \texttt{runJsonRpcT}
corresponds to being connected to one JSON-RPC endpoint, since it takes
a single sink and source. But what if we want our application to be
connected to multiple endpoints at the same time?

What if we need to have hundreds of thousands of these, and we want to
store them in some priority queue and only consider the most recent ones
in the general case. How would you go about that?

You could consider running a lightweight thread for every
\texttt{runJsonRpcT}, but that means you now need to worry about thread
overhead, communicating exceptions between threads and killing the
threads after you remove them. Whereas with first-class handles, we
would just have a \texttt{HashPSQ\ Text\ Int\ JsonRpc.Handle}, which is
much easier to reason about.

\begin{center}\rule{0.5\linewidth}{\linethickness}\end{center}

\noindent So, I guess one of the oldest and most widely used approaches is
MTL-style monad transformers. This uses a hierarchy of typeclasses to
represent access to various subsystems.

I love working with MTL-style transformers in the case of pure code,
since they often allow us to express complex ideas concisely. For
effectful code, on the other hand, they do not seem to offer many
advantages and often make it harder to reason about code.

My personal preference for writing complex effectful code is to reify
the effectful operations as a datatype and then write pure code
manipulating these effectful operations. An interpreter can then simply
use the \texttt{Handle}s to perform the effects. For simpler effectful
code, we can just use \texttt{Handle}s directly.

I have implemented a number of these patterns in the (ever unfinished)
example web application
\href{https://github.com/jaspervdj/fugacious}{fugacious}, in case you
want to see them in action or if you want a more elaborate example than
the short snippets in this blogpost. Finally, I would like to thank
\href{https://github.com/alang9/}{Alex Lang} and
\href{http://www.nmattia.com/}{Nicolas Mattia} for proofreading, and
\href{https://github.com/tivervac}{Titouan Vervack} for many corrections
and typos.
